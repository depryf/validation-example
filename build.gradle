import org.gradle.internal.jvm.Jvm

// apply the script plugins
plugins {
    id 'java'
}

// project properties
group = 'com.imsweb'
version = '1.0'
description = 'Your project description here.'
sourceCompatibility = JavaVersion.VERSION_1_8

// print which version of Java is being used
println "Starting build process using Java ${Jvm.current()}"

// where to find the project dependencies
repositories {
    mavenCentral()
}

// project dependencies
dependencies {
    implementation 'com.imsweb:validation:1.20'
    implementation 'com.imsweb:validation-edits-seer:018-05'
    implementation 'com.imsweb:staging-algorithm-cs:02.05.50'
    implementation 'com.imsweb:staging-algorithm-tnm:1.9'
    implementation 'com.imsweb:staging-algorithm-eod-public:1.3'
    implementation 'com.imsweb:layout:1.12'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

// Gradle wrapper, this allows to build the project without having to install Gradle...
wrapper {
    gradleVersion = '4.9'
}


